// Test output.
import void testPrint(const ubyte * str, ...);

class TestClass (int first, int second) {
	static Create(int first, int second) {
		testPrint(C"TestClass.Create: First = %d, Second = %d", first, second);
		return @(first, second);
	}
	
	int getFirst() {
		testPrint(C"TestClass.getFirst: First = %d", @first);
		return @first;
	}
	
	int getSecond() {
		testPrint(C"TestClass.getSecond: Second = %d", @second);
		return @second;
	}
	
}

interface TestInterfaceBoth {
	int getFirst();
	
	int getSecond();
}

interface TestInterfaceFirstOnly {
	int getFirst();
}

interface TestInterfaceSecondOnly {
	int getSecond();
}

void polymorphicPrintFirst(TestInterfaceFirstOnly& object) {
	int value = object.getFirst();
	testPrint(C"polymorphicPrintFirst: Value = %d", value);
}

void polymorphicPrintSecond(TestInterfaceSecondOnly& object) {
	int value = object.getSecond();
	testPrint(C"polymorphicPrintSecond: Value = %d", value);
}

void polymorphicPrintBoth(TestInterfaceBoth& object) {
	int first = object.getFirst();
	int second = object.getSecond();
	testPrint(C"polymorphicPrintBoth: First = %d, Second = %d", first, second);
	
	polymorphicPrintFirst(object);
	polymorphicPrintSecond(object);
}

// Entry point.
export int testEntryPoint(unused int argc, unused ubyte ** argv) {
	auto testInstance = TestClass(5, 6);
	
	polymorphicPrintBoth(testInstance);
	
	return 0;
}

