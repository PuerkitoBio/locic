// Test output.
import void testPrint(const ubyte * str, ...) noexcept;

class TestClass(int value) {
	static create(int value) {
		testPrint(C"Create TestClass: %d", value);
		return @(value);
	}
	
	~ {
		testPrint(C"Destroy TestClass: %d", @value);
	}
}

class OwnerClass(int id, std::shared_ptr<TestClass> ptr) {
	static create(int id, std::shared_ptr<TestClass> ptr) {
		testPrint(C"Create owner%d.", id);
		return @(id, move ptr);
	}
	
	~ {
		testPrint(C"Destroy owner%d.", @id);
	}
	
	OwnerClass implicitCopy() const {
		int newId = 2;
		testPrint(C"Copy owner%d as owner%d.", @id, newId);
		return @(newId, @ptr);
	}
}

// Entry point.
export int testEntryPoint(unused int argc, unused ubyte ** argv) {
	auto sharedPtr = std::new_shared<TestClass>(TestClass(10));
	
	auto owner0 = OwnerClass(0, sharedPtr);
	auto owner1 = OwnerClass(1, sharedPtr);
	
	{
		unused auto owner2 = owner0;
	}
	
	unused_result move sharedPtr;
	unused_result move owner0;
	unused_result move owner1;
	
	testPrint(C"All pointers destroyed...");
	
	return 0;
}

