export std.memory 0.1.0 {
	
	namespace std {
		
		template <typename T>
		class unique_ptr(T* ptr) {
			static create(T value) require(T : movable) {
				return @(new_raw<T>(move value));
			}
			
			static null() noexcept {
				return @(null);
			}
			
			static __dead() noexcept {
				return @(null);
			}
			
			~ {
				if (@ptr != null) {
					delete_raw<T>(@ptr);
				}
			}
			
			bool __is_live() const noexcept {
				return @ptr != null;
			}
			
			template <bool IsConst>
			lval<const<IsConst>(T)> ptr_lval<const<IsConst>(T)> deref() const(IsConst) noexcept {
				return *@ptr;
			}
			
			template <bool IsConst>
			const<IsConst>(T)* get() const(IsConst) noexcept {
				return @ptr;
			}
			
			void reset(T* pointer) noexcept {
				if (@ptr != null) {
					delete_raw<T>(@ptr);
				}
				@ptr = pointer;
			}
			
			T* release() noexcept {
				T* pointer = @ptr;
				@ptr = null;
				return pointer;
			}
		}
		
		template <typename T : movable>
		unique_ptr<T> new_unique(T value) {
			return unique_ptr<T>(move value);
		}
		
	}
	
}

